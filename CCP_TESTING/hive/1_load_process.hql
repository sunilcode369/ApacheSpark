--
dfs -copyFromLocal ./patient_data.csv /user/cloudera/ccp/.;
CREATE DATABASE IF NOT EXISTS PatientInfo;
use PatientInfo;

CREATE TABLE IF NOT EXISTS PatientInfo.patientdetail_TEMP(  FIRSTNAME STRING, LASTNAME STRING,
    		          ADDRESS STRUCT<HOUSENO:STRING,LOCALITYNAME:STRING,CITY:STRING>,
		           mobile STRING, LANDLINE STRING, ZIP STRING)
ROW FORMAT DELIMITED
FIELDS TERMINATED BY '|'
COLLECTION ITEMS TERMINATED BY ','
LINES TERMINATED BY '\n'
STORED AS TEXTFILE;

LOAD DATA INPATH '/user/cloudera/ccp/patient_data.csv' INTO TABLE patientdetail_TEMP;

CREATE TABLE IF NOT EXISTS PatientInfo.patientdetail( NAME STRUCT<FIRSTNAME:STRING,LASTNAME:STRING>,
    		          ADDRESS STRUCT<HOUSENO:STRING,LOCALITYNAME:STRING,CITY:STRING,ZIP:STRING>,
		          PHONE STRUCT<MOBILE:STRING,LANDLINE:STRING> )
ROW FORMAT DELIMITED
FIELDS TERMINATED BY '|'
COLLECTION ITEMS TERMINATED BY ','
LINES TERMINATED BY '\n'
STORED AS SEQUENCEFILE
LOCATION '/user/hive/warehouse/patient';

INSERT INTO TABLE patientdetail select named_struct("FIRSTNAME",FIRSTNAME,"LASTNAME",LASTNAME),named_struct("HOUSENO",ADDRESS.HOUSENO,"LOCALITYNAME",ADDRESS.LOCALITYNAME,"CITY",ADDRESS.CITY,"ZIP",ZIP),named_struct("MOBILE",MOBILE,"LANDLINE",LANDLINE) from patientdetail_Temp;

DROP TABLE PatientInfo.patientdetail_TEMP;

